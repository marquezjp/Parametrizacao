-- Corpo do pacote
CREATE OR REPLACE PACKAGE BODY PKGMIG_ConfiguracaoPadrao AS

  FUNCTION fnObterNivelDebug(pDEBUG IN VARCHAR2) RETURN PLS_INTEGER IS
  BEGIN
  CASE UPPER(TRIM(NVL(pDEBUG, 'DESLIGADO')))
    WHEN 'DEBUG NIVEL 0' THEN RETURN cDEBUG_NIVEL_0;
    WHEN 'DEBUG NIVEL 1' THEN RETURN cDEBUG_NIVEL_1;
    WHEN 'DEBUG NIVEL 2' THEN RETURN cDEBUG_NIVEL_2;
    WHEN 'DESLIGADO'     THEN RETURN cDEBUG_DESLIGADO;
    ELSE RETURN cDEBUG_DESLIGADO;
  END CASE;
  END;

  PROCEDURE pExportar(pSgAgrupamento IN VARCHAR2,
    psgConceito IN VARCHAR2, pDEBUG IN VARCHAR2 DEFAULT NULL) IS
    vnuDEBUG NUMBER := fnObterNivelDebug(pDEBUG);
  BEGIN
    CASE UPPER(psgConceito)
      WHEN 'VALORREFERENCIA' THEN
        PKGMIG_ExportarValoresReferencia.pExportar(psgAgrupamento, vnuDEBUG);
--      WHEN 'BASE' THEN
--        PKGMIG_ExportarBasesCalculo.pExportar(psgAgrupamento, vnuDEBUG);
--      WHEN 'RUBRICA' THEN
--        PKGMIG_ExportarRubricas.pExportar(psgAgrupamento);
      ELSE
        RAISE_APPLICATION_ERROR(-20001, 'Conceito não suportado: ' || psgConceito);
    END CASE;
  END pExportar;

  PROCEDURE pImportar(psgAgrupamentoOrigem IN VARCHAR2, psgAgrupamentoDestino IN VARCHAR2,
    psgConceito IN VARCHAR2, pDEBUG IN VARCHAR2 DEFAULT NULL) IS
    vnuDEBUG NUMBER := fnObterNivelDebug(pDEBUG);
  BEGIN
    CASE UPPER(psgConceito)
      WHEN 'VALORREFERENCIA' THEN
        PKGMIG_ImportarValoresReferencia.pImportar(psgAgrupamentoOrigem, psgAgrupamentoDestino, vnuDEBUG);
--      WHEN 'BASE' THEN
--        PKGMIG_ImportarBasesCalculo.pImportar(psgAgrupamentoOrigem, psgAgrupamentoDestino, vnuDEBUG);
--      WHEN 'RUBRICA' THEN
--        PKGMIG_ImportarRubricas.pImportar(psgAgrupamentoOrigem, psgAgrupamentoDestino, vnuDEBUG);
      ELSE
        RAISE_APPLICATION_ERROR(-20002, 'Importação não suportada para o conceito: ' || psgConceito);
    END CASE;
  END pImportar;

  PROCEDURE pConsoleLog(
  -- ###########################################################################
  -- PROCEDURE: pConsoleLog
  -- Objetivo:
  --   Mostrar os eventos de auditoria na saída padrão, Console.
  --
  -- Parâmetros:
  --   pdeMensagem      IN VARCHAR2:  'Mensagem detalhada sobre a operação registrada.'
  --   pDEBUG           IN VARCHAR2 DEFAULT NULL: Defini o nível das mensagens
  --                         para acompanhar a execução, sendo:
  --                         - Não informado assume 'Desligado' nível mínimo de mensagens;
  --                         - Se informado 'DEBUG NIVEL 0' omite todas as mensagens;
  --                         - Se informado 'DEBUG NIVEL 1' inclui as mensagens das
  --                           principais todas entidades, menos as listas;
  --                         - Se informado 'DEBUG NIVEL 2' inclui as mensagens de todas 
  --                           entidades, incluindo as referente as tabelas das listas;
  --
  -- ###########################################################################
    pdeMensagem      IN VARCHAR2,
    pnuNivelLog      IN NUMBER DEFAULT NULL,
    pnuDEBUG         IN NUMBER DEFAULT NULL
    ) IS
    BEGIN
      -- Incluir Log de Auditoria da Importação das Configurações Padrão
      IF NVL(pnuDEBUG, cDEBUG_DESLIGADO) >= NVL(pnuNivelLog, cDEBUG_DESLIGADO) THEN
	    DBMS_OUTPUT.PUT_LINE(pdeMensagem);
      END IF;
  END pConsoleLog;

  PROCEDURE pRegistrarLog(
  -- ###########################################################################
  -- PROCEDURE: pRegistrarLog
  -- Objetivo:
  --   Registrar eventos de auditoria, controle ou estatística no processo de
  --   importação de configurações padrão (rubricas, bases, valores, etc.).
  --   Os registros são inseridos na tabela emigConfiguracaoPadraoLog.
  --
  -- Parâmetros:
  --   psgAgrupamento   IN VARCHAR2:  'Sigla do Agrupamento relacionado ao evento.'
  --   psgOrgao         IN VARCHAR2:  'Sigla do Órgão, se nulo se refere a todos os órgãos.'
  --   ptpOperacao      IN VARCHAR2:  'Se o evento é de EXPORTACAO ou de IMPORTACAO.'
  --   pdtOperacao      IN TIMESTAMP: 'Data e hora do incio da operação relacionada a todos os evento da operação.'
  --   psgModulo        IN CHAR:      'Sigla do Módulo que originou o evento .'
  --   psgConceito      IN VARCHAR2:  'Conceito associado à parametrização.'
  --   pcdIdentificacao IN VARCHAR2:  'Código Identificador da Entidade Parametrizada afetada pela operação.'
  --   nmEntidade       IN VARCHAR2:  'Entidade dentro do Conceito associado à parametrização, grupo de informações do Conceito.'
  --   pnmEvento        IN VARCHAR2:  'Nome do evento (INCLUSAO, ATUALIZACAO, EXCLUSAO, RESUMO).'
  --   pdeMensagem      IN VARCHAR2:  'Mensagem detalhada sobre a operação registrada.'
  --   pDEBUG           IN VARCHAR2 DEFAULT NULL: Defini o nível das mensagens
  --                         para acompanhar a execução, sendo:
  --                         - Não informado assume 'Desligado' nível mínimo de mensagens;
  --                         - Se informado 'DEBUG NIVEL 0' omite todas as mensagens;
  --                         - Se informado 'DEBUG NIVEL 1' inclui as mensagens das
  --                           principais todas entidades, menos as listas;
  --                         - Se informado 'DEBUG NIVEL 2' inclui as mensagens de todas 
  --                           entidades, incluindo as referente as tabelas das listas;
  --
  -- ###########################################################################
    psgAgrupamento   IN VARCHAR2,
    psgOrgao         IN VARCHAR2,
    ptpOperacao      IN VARCHAR2,
    pdtOperacao      IN TIMESTAMP,
    psgModulo        IN CHAR,
    psgConceito      IN VARCHAR2,
    pcdIdentificacao IN VARCHAR2,
    pnuRegistros     IN NUMBER,
    pnmEntidade      IN VARCHAR2,
    pnmEvento        IN VARCHAR2,
    pdeMensagem      IN VARCHAR2,
    pnuNivelLog      IN NUMBER DEFAULT NULL,
    pnuDEBUG         IN NUMBER DEFAULT NULL
    ) IS
      PRAGMA AUTONOMOUS_TRANSACTION;
    BEGIN
      -- Incluir Log de Auditoria da Importação das Configurações Padrão
      IF NVL(pnuDEBUG, cDEBUG_DESLIGADO) >= NVL(pnuNivelLog, cDEBUG_DESLIGADO) THEN
        INSERT INTO emigConfiguracaoPadraoLog (
          sgAgrupamento, sgOrgao, sgModulo, sgConceito, tpOperacao, dtOperacao,
          nmEntidade, cdIdentificacao, nmEvento, nuRegistros, deMensagem
        ) VALUES (
          psgAgrupamento, psgOrgao, psgModulo, psgConceito, ptpOperacao, pdtOperacao,
          pnmEntidade, pcdIdentificacao, pnmEvento, pnuRegistros, pdeMensagem
        );
        COMMIT;
      END IF;
  END pRegistrarLog;

  PROCEDURE PAtualizarSequence(
  -- ###########################################################################
  -- PROCEDURE: PAtuializarSequence
  -- Objetivo:
  --   Atualizar a SEQUENCE com o Maior Número da Chave Primaria
  --     das tabelas envolvidas na importação das Rubricas do Agrupamento
  --
  -- Parâmetros:
  --   psgAgrupamento        IN VARCHAR2:
  --   psgOrgao              IN VARCHAR2:
  --   ptpOperacao           IN VARCHAR2:
  --   pdtOperacao           IN TIMESTAMP:
  --   psgModulo             IN CHAR: 
  --   psgConceito           IN VARCHAR2: 
  --   pnuDEBUG              IN NUMBER DEFAULT NULL:
  --
  -- ###########################################################################
    psgAgrupamento        IN VARCHAR2,
    psgOrgao              IN VARCHAR2,
    ptpOperacao           IN VARCHAR2,
    pdtOperacao           IN TIMESTAMP,
    psgModulo             IN CHAR,
    psgConceito           IN VARCHAR2,
--    pListaTabelas         IN tpLista,
	pnuDEBUG              IN NUMBER DEFAULT NULL
  ) IS
    -- Variáveis de controle e contexto
      vtxSQL VARCHAR2(1000);
      vnuRegistros NUMBER;

    -- Cursor que extrai as Vigências da Rubrica do Agrupamento do Documento pVigenciasAgrupamento JSON
    CURSOR cDados IS
      SELECT tab.table_name, col.column_name, seq.sequence_name, seq.last_number, tab.num_rows
      FROM user_tables tab
      LEFT JOIN user_sequences seq ON SUBSTR(seq.sequence_name,2) = SUBSTR(tab.table_name,2)
      LEFT JOIN all_tab_columns col ON col.table_name = tab.table_name AND col.column_id = 1
      WHERE seq.sequence_name IS NOT NULL;
--         AND tab.table_name MEMBER OF pListaTabelas;
--       AND tab.table_name in (
--        'EPAGRUBRICA', 'EPAGGRUPORUBRICAPAGAMENTO', 'EPAGHISTRUBRICA',
--        'EPAGRUBRICAAGRUPAMENTO', 'EPAGHISTRUBRICAAGRUPAMENTO', 'EPAGHISTRUBRICAAGRUPNATVINC',
--        'EPAGHISTRUBRICAAGRUPREGPREV', 'EPAGHISTRUBRICAAGRUPREGTRAB', 'EPAGHISTRUBRICAAGRUPRELTRAB', 'EPAGHISTRUBRICAAGRUPSITPREV',
--        'EPAGEVENTOPAGAGRUP', 'EPAGHISTEVENTOPAGAGRUP', 'EPAGEVENTOPAGAGRUPORGAO', 'EPAGHISTEVENTOPAGAGRUPCARREIRA',
--        'EPAGFORMULACALCULO', 'EPAGFORMULAVERSAO', 'EPAGHISTFORMULACALCULO', 'EPAGEXPRESSAOFORMCALC',
--        'EPAGFORMULACALCULOBLOCO', 'EPAGFORMULACALCBLOCOEXPRESSAO', 'EPAGFORMCALCBLOCOEXPRUBAGRUP'
--      );

    BEGIN
      FOR item IN cDados
        LOOP
          -- Obtendo o Maior Número da Chave Primaria da Tabela
          vtxSQL := 'SELECT NVL(MAX(' || item.column_name || '), 0) + 1 FROM ' || item.table_name;
          EXECUTE IMMEDIATE vtxSQL INTO vnuRegistros;

          PConsoleLog('Atualizar a SEQUENCE ' || item.sequence_name || ' da Tabela ' || item.table_name ||
            ' reiniciar com ' || vnuRegistros );

          -- Atualizar a SEQUENCE com o Maior Número da Chave Primaria da Tabela
          execute immediate 'alter sequence ' || item.sequence_name || ' restart start with ' || case when vnuRegistros = 0 then 1 else vnuRegistros end;
          execute immediate 'analyze table ' || upper(item.table_name) || ' compute statistics';

          pRegistrarLog(psgAgrupamento, psgOrgao, ptpOperacao, pdtOperacao,  
            psgModulo, psgConceito, NULL, NULL, 'SEQUENCE', NULL,
            'Atualizar a SEQUENCE ' || item.sequence_name || ' da Tabela ' || item.table_name ||
            ' para reiniciar em: ' || vnuRegistros,
            cDEBUG_DESLIGADO, pnuDEBUG);
      END LOOP;

  EXCEPTION
    WHEN OTHERS THEN
      -- Registro e Propagação do Erro
      PConsoleLog('Atualizar as SEQUENCE da Importação das Rubrica RUBRICA SEQUENCE Erro: ' || SQLERRM);
      pRegistrarLog(psgAgrupamento, psgOrgao, ptpOperacao, pdtOperacao, 
        psgModulo, psgConceito, NULL, NULL,
        'SEQUENCE', 'ERRO', 'Erro: ' || SQLERRM,
        cDEBUG_DESLIGADO, pnuDEBUG);
    ROLLBACK;
    RAISE;
  END PAtualizarSequence;

  PROCEDURE pGerarResumo(
  -- ###########################################################################
  -- PROCEDURE: pGerarResumo
  -- Objetivo:
  --   Apura as informações estatísticas do processo de Importar
  --   contida na tabela emigConfiguracaoPadraoLog, realizando:
  --     - Consolida e Contabiliza os Registros Excluídos, Atualizados, Incluídos
  --       e Inconsistências encontradas.
  --     - Gerar Registro de Logs com resumo dos evento
  --
  -- Parâmetros:
  --   psgAgrupamento        IN VARCHAR2:
  --   psgOrgao              IN VARCHAR2,
  --   ptpOperacao           IN VARCHAR2,
  --   pdtOperacao           IN TIMESTAMP,
  --   psgModulo             IN CHAR,
  --   psgConceito           IN VARCHAR2,
  --   pdtTermino            IN TIMESTAMP,
  --   pnuTempoExcusao       IN INTERVAL DAY TO SECOND,
  --   pnuDEBUG              IN NUMBER DEFAULT NULL:
  --
  -- ###########################################################################
    psgAgrupamento        IN VARCHAR2,
    psgOrgao              IN VARCHAR2,
    ptpOperacao           IN VARCHAR2,
    pdtOperacao           IN TIMESTAMP,
    psgModulo             IN CHAR,
    psgConceito           IN VARCHAR2,
    pdtTermino            IN TIMESTAMP,
    pnuTempoExcusao       IN INTERVAL DAY TO SECOND,
    pnuDEBUG              IN NUMBER DEFAULT NULL
  ) IS
    -- Variáveis de controle e contexto
    vResumoEstatisticas      CLOB := Null;

    -- Cursor que extrai as estatísticas do Log
    CURSOR cLog IS
      WITH
      LOG AS (
      SELECT sgAgrupamento, sgOrgao, sgModulo, sgConceito, tpOperacao, dtOperacao, nmEntidade, nmEvento, 1 as nuRegistros,
        CASE nmEvento WHEN 'INCLUSAO' THEN 1 WHEN 'ATUALIZACAO' THEN 2 WHEN 'EXCLUSAO' THEN 3 ELSE 9 END AS cdEvento,
        CASE WHEN nmEvento != 'EXCLUSAO' THEN dtInclusao ELSE TO_TIMESTAMP('99991231 235959', 'YYYYMMDD HH24MISS')
          END AS dtInclusaoAjustada
      FROM emigConfiguracaoPadraolog
      WHERE sgModulo = psgModulo AND sgConceito = psgConceito AND nmEvento != 'RESUMO'
        AND tpOperacao = ptpOperacao AND dtOperacao = pdtOperacao
        AND sgAgrupamento = psgAgrupamento
      ),
      OrdemEntidade AS (
      SELECT nmEntidade, RANK() OVER(ORDER BY dtInclusaoAjustada) AS cdEntidade FROM (
        SELECT nmEntidade, MIN(dtInclusaoAjustada) as dtInclusaoAjustada FROM Log GROUP BY nmEntidade
      ) ORDER BY dtInclusaoAjustada
      ),
      Estatisticas AS (
      SELECT log.sgAgrupamento, log.sgOrgao, log.sgModulo, log.sgConceito, log.tpOperacao,
        log.dtOperacao, ordem.cdEntidade, log.nmEntidade, log.cdEvento, SUM(nuRegistros) AS nuRegistros
      FROM LOG log
      INNER JOIN OrdemEntidade ordem ON ordem.nmEntidade = log.nmEntidade
      GROUP BY log.sgAgrupamento, log.sgOrgao, log.sgModulo, log.sgConceito,
      log.tpOperacao, log.dtOperacao, ordem.cdEntidade, log.nmEntidade, log.cdEvento
      ),
      Resumo AS (
      SELECT sgAgrupamento, sgOrgao, sgModulo, sgConceito, tpOperacao, dtOperacao,
      cdEntidade, nmEntidade, Incluidos, Atualizados, Excluidos, Outros
      FROM Estatisticas
      PIVOT (SUM(nuRegistros) FOR cdEvento IN (1 AS Incluidos, 2 As Atualizados, 3 AS Excluidos, 9 AS Outros))
      ORDER BY sgAgrupamento, sgOrgao, sgModulo, sgConceito, nmEntidade
      )
      SELECT JSON_SERIALIZE (TO_CLOB(JSON_OBJECT(
        sgModulo VALUE JSON_OBJECT(
          sgConceito VALUE JSON_OBJECT(
            tpOperacao,
            sgAgrupamento,
            'sgOrgao' value NVL(sgOrgao,'TODOS'),
            'dtOperacaoInicio' VALUE TO_CHAR(dtOperacao, 'DD/MM/YYYY HH24:MI:SS'),
            'dtOperacaoTermino' VALUE TO_CHAR(pdtTermino, 'DD/MM/YYYY HH24:MI:SS'),
            'TempoExceusao' VALUE
			  LPAD(EXTRACT(HOUR FROM pnuTempoExcusao), 2, '0') || ':' ||
			  LPAD(EXTRACT(MINUTE FROM pnuTempoExcusao), 2, '0') || ':' ||
			  LPAD(EXTRACT(SECOND FROM pnuTempoExcusao), 2, '0'),
            'Registros' VALUE JSON_ARRAYAGG(JSON_OBJECT(
              nmEntidade VALUE JSON_OBJECT(
                'Excluídos' VALUE Excluidos,
                'Atualizados' VALUE Atualizados,
                'Incluídos' VALUE Incluidos,
                'Outros' VALUE Outros
              ABSENT ON NULL)
            ) ORDER By cdEntidade)
          RETURNING CLOB)
        RETURNING CLOB)
      RETURNING CLOB)) RETURNING CLOB PRETTY) AS ResumoEstatisticas
      FROM Resumo
      GROUP BY sgAgrupamento, sgOrgao, dtOperacao, tpOperacao, sgModulo, sgConceito;

  BEGIN

    -- Consolida as Informações de Estatísticas da Importação das Rubricas
    OPEN cLog;
    FETCH cLog INTO vResumoEstatisticas;
    CLOSE cLog;

    -- Registro de Resumo da Importação das Rubricas
    pRegistrarLog(psgAgrupamento, psgOrgao, ptpOperacao, pdtOperacao,
      psgModulo, psgConceito, NULL, NULL,
      'RESUMO', 'RESUMO', vResumoEstatisticas,
      cDEBUG_DESLIGADO, pnuDEBUG);

    PConsoleLog('Resumo da Importação das Configurações do Agrupamento ' || psgAgrupamento);

    PConsoleLog('Estatísticas: ' || vResumoEstatisticas);

  EXCEPTION
    WHEN OTHERS THEN
      -- Registro e Propagação do Erro
      PConsoleLog('Resumo da Importação das Rubrica RUBRICA RESUMO Erro: ' || SQLERRM);
      pRegistrarLog(psgAgrupamento, psgOrgao, ptpOperacao, pdtOperacao,  
        psgModulo, psgConceito, NULL, NULL,
        'RUBRICA', 'ERRO', 'Erro: ' || SQLERRM,
        cDEBUG_DESLIGADO, pnuDEBUG);
    ROLLBACK;
    RAISE;
  END pGerarResumo;

-- Resumo das Operações de Exportação das Configurações
  FUNCTION fnResumo(
    psgAgrupamento   IN VARCHAR2 DEFAULT NULL,
    psgOrgao         IN VARCHAR2 DEFAULT NULL,
    psgModulo        IN CHAR     DEFAULT NULL,
    psgConceito      IN VARCHAR2 DEFAULT NULL,
    pdtExportacao    IN VARCHAR2 DEFAULT NULL
  ) RETURN tpConfiguracaoResumoTabela PIPELINED
  IS
  BEGIN
    FOR r IN (
      SELECT sgAgrupamento, sgOrgao, sgModulo, sgConceito, TO_CHAR(dtExportacao, 'DD/MM/YYYY HH24:MI:SS') AS dtExportacao, COUNT(*) AS Conteudos
      FROM emigConfiguracaoPadrao
      WHERE (sgAgrupamento LIKE psgAgrupamento OR psgAgrupamento IS NULL)
        AND (sgOrgao LIKE psgOrgao OR psgOrgao IS NULL)
        AND (sgModulo LIKE psgModulo OR psgModulo IS NULL)
        AND (sgConceito LIKE psgConceito OR psgConceito IS NULL)
        AND (TO_CHAR(dtExportacao, 'DD/MM/YYYY HH24:MI:SS') LIKE pdtExportacao OR pdtExportacao IS NULL)
      GROUP BY sgAgrupamento, sgOrgao, sgModulo, sgConceito, dtExportacao
      ORDER BY sgAgrupamento, sgOrgao, sgModulo, sgConceito, dtExportacao desc)
    LOOP
      PIPE ROW (tpConfiguracaoResumo(r.sgAgrupamento, r.sgOrgao,
        r.sgModulo, r.sgConceito, r.dtExportacao,
        r.Conteudos));
    END LOOP;
    RETURN;
  END fnResumo;

-- Listar a Exportação das Configurações
  FUNCTION fnListar(
    psgAgrupamento   IN VARCHAR2,
    psgOrgao         IN VARCHAR2,
    psgModulo        IN CHAR,
    psgConceito      IN VARCHAR2,
    pdtExportacao    IN VARCHAR2
  ) RETURN tpConfiguracaoListarTabela PIPELINED
  IS
  BEGIN
    FOR r IN (
      SELECT sgAgrupamento, sgOrgao, sgModulo, sgConceito, TO_CHAR(dtExportacao, 'DD/MM/YYYY HH24:MI:SS') AS dtExportacao, cdIdentificacao,
        JSON_SERIALIZE(TO_CLOB(jsconteudo) RETURNING CLOB PRETTY) AS jsConteudo
      FROM emigConfiguracaoPadrao
      WHERE (sgAgrupamento = psgAgrupamento)
        AND (NVL(sgOrgao, ' ') = NVL(psgOrgao, ' '))
        AND (sgModulo = psgModulo)
        AND (sgConceito = psgConceito)
        AND (TO_CHAR(dtExportacao, 'DD/MM/YYYY HH24:MI:SS') LIKE pdtExportacao OR pdtExportacao IS NULL)
      ORDER BY sgAgrupamento, sgOrgao, sgModulo, sgConceito)
    LOOP
      PIPE ROW (tpConfiguracaoListar(r.sgAgrupamento, r.sgOrgao,
          r.sgModulo, r.sgConceito, r.dtExportacao,
          r.cdIdentificacao, r.jsConteudo));
    END LOOP;
    RETURN;
  END fnListar;

-- Resumo do Log das Operações de Exportação e Importação das Configurações
  FUNCTION fnResumoLog(
    psgAgrupamento   IN VARCHAR2  DEFAULT NULL,
    psgOrgao         IN VARCHAR2  DEFAULT NULL,
    psgModulo        IN CHAR      DEFAULT NULL,
    psgConceito      IN VARCHAR2  DEFAULT NULL,
    ptpOperacao      IN VARCHAR2  DEFAULT NULL
  ) RETURN tpConfiguracaoLogResumoTabela PIPELINED
  IS
  BEGIN
    FOR r IN (
      SELECT DISTINCT tpOperacao, TO_CHAR(dtOperacao, 'DD/MM/YYYY HH24:MI:SS') AS dtOperacao,
        sgAgrupamento, sgOrgao, sgModulo, sgConceito
      FROM emigConfiguracaoPadraoLog
      WHERE (tpOperacao LIKE ptpOperacao OR ptpOperacao IS NULL)
        AND (sgAgrupamento LIKE psgAgrupamento OR psgAgrupamento IS NULL)
        AND (sgOrgao LIKE psgOrgao OR psgOrgao IS NULL)
        AND (sgModulo LIKE psgModulo OR psgModulo IS NULL)
        AND (sgConceito LIKE psgConceito OR psgConceito IS NULL)
      ORDER BY tpOperacao, sgAgrupamento, sgOrgao, sgModulo, sgConceito, dtOperacao)
    LOOP
      PIPE ROW (tpConfiguracaoLogResumo(r.tpOperacao, r.dtOperacao,
        r.sgAgrupamento, r.sgOrgao, r.sgModulo, r.sgConceito));
    END LOOP;
    RETURN;
  END fnResumoLog;

-- Resumo do Log das Operações de Exportação e Importação das Configurações
  FUNCTION fnResumoLogEntidades(
    ptpOperacao      IN VARCHAR2,
    pdtOperacao      IN VARCHAR2
  ) RETURN tpConfiguracaoLogResumoEntidadesTabela PIPELINED
  IS
  BEGIN
    FOR r IN (
      SELECT tpOperacao, TO_CHAR(dtOperacao, 'DD/MM/YYYY HH24:MI:SS') AS dtOperacao,
	    sgAgrupamento, sgOrgao, sgModulo, sgConceito, nmEntidade, COUNT(*) as nuEventos, SUM(nuRegistros) as nuRegistros
      FROM emigConfiguracaoPadraoLog
      WHERE nmevento != 'RESUMO'
        AND tpOperacao = ptpOperacao
        AND TO_CHAR(dtOperacao, 'YYYYMMDDHH24MISS') = pdtOperacao
      GROUP BY tpOperacao, dtOperacao, sgAgrupamento, sgOrgao, sgModulo, sgConceito, nmEntidade
      ORDER BY tpOperacao, dtOperacao, sgAgrupamento, sgOrgao, sgModulo, sgConceito, nmEntidade)
    LOOP
      PIPE ROW (tpConfiguracaoLogResumoEntidades(r.tpOperacao, r.dtOperacao,
	    r.sgAgrupamento, r.sgOrgao, r.sgModulo, r.sgConceito,
		r.nmEntidade, r.nuEventos, r.nuRegistros));
    END LOOP;
    RETURN;
  END fnResumoLogEntidades;

-- Listar o Log da Operação de Exportação ou Importação das Configurações
  FUNCTION fnListarLog(
    ptpOperacao      IN VARCHAR2,
    pdtOperacao      IN VARCHAR2
  ) RETURN tpConfiguracaoLogListarTabela PIPELINED
  IS
  BEGIN
    FOR r IN (
      SELECT tpOperacao, TO_CHAR(dtOperacao, 'DD/MM/YYYY HH24:MI:SS') AS dtOperacao,
        sgAgrupamento, sgOrgao, sgModulo, sgConceito,
        nmEntidade, cdidentificacao, nmEvento, nuRegistros, deMensagem, dtInclusao
      FROM emigConfiguracaoPadraoLog
      WHERE tpOperacao = ptpOperacao
        AND TO_CHAR(dtOperacao, 'YYYYMMDDHH24MISS') = pdtOperacao
      ORDER BY dtInclusao, tpOperacao, dtOperacao,
        sgAgrupamento, sgOrgao, sgModulo, sgConceito,
        cdIdentificacao NULLS FIRST, nmEvento, deMensagem)
    LOOP
      PIPE ROW (tpConfiguracaoLogListar(r.tpOperacao, r.dtOperacao,
        r.sgAgrupamento, r.sgOrgao, r.sgModulo, r.sgConceito,
        r.nmEntidade, r.cdidentificacao, r.nmEvento, r.nuRegistros, r.deMensagem, r.dtInclusao));
    END LOOP;
    RETURN;
  END fnListarLog;

-- Excluir o Log da Operação de Exportação ou Importação das Configurações
  PROCEDURE PExcluirLog(
    ptpOperacao      IN VARCHAR2,
    pdtOperacao      IN VARCHAR2,
    psgAgrupamento   IN VARCHAR2,
    psgModulo        IN CHAR,
    psgConceito      IN VARCHAR2
  ) IS
  BEGIN
    DELETE FROM emigConfiguracaoPadraoLog
      WHERE tpOperacao = ptpOperacao
        AND TO_CHAR(dtOperacao, 'YYYYMMDDHH24MISS') = pdtOperacao
        AND sgAgrupamento = psgAgrupamento
        AND sgModulo = psgModulo AND sgConceito = psgConceito;
    COMMIT;
  END PExcluirLog;

END PKGMIG_ConfiguracaoPadrao;
/

